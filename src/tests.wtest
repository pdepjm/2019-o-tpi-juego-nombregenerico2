import wollok.game.*
import actores.*
import clasesBase.*

describe "Colisiones" {

	const unMontable = new Montable(direccion = izquierda, image = "tronco/2.png", position = game.at(0, 0), velocidad = 500)
	const autoA1 = new Obstaculo()
	const rana1P = new Rana(posicionInicial = game.at(5, 1), nombreSprite = "rana",otraRana = rana1P)

	test "la rana pierda una vida si la chocan" {
		const vidasIniciales = rana1P.vidas()
		autoA1.colisionarConUnaRana(rana1P)
		assert.equals(rana1P.vidas(), vidasIniciales - 1)
	// en este caso estaria bueno poder ver si comienza de nuevo en el mismo lugar
	}

	test "Un montable mueve a la rana si esta colisionando con ella y moviendose" {
		const posicionMontable = game.at(3, 3)
		unMontable.position(posicionMontable)
		game.width(14)
		game.height(16)
		game.addVisual(unMontable)
		game.addVisual(rana1P)
		rana1P.position(posicionMontable)
		unMontable.moverse()
		assert.equals(rana1P.position(), posicionMontable.left(1))
	}

	test "Rana no se puede salir del tablero al tratar de moverse por si sola" {
		game.width(14)
		game.height(16)
		game.addVisual(rana1P)
		const posicionInicial = game.at(0, 0)
		rana1P.position(posicionInicial)
		rana1P.left()
		assert.equals(rana1P.position(), posicionInicial)
	}

	test "Un montable no puede sacar a la rana del tablero al moverse con ella" {
		game.width(14)
		game.height(16)
		game.addVisual(unMontable)
		game.addVisual(rana1P)
		const posicionMontableInicial = game.at(0, 0)
		rana1P.position(posicionMontableInicial)
		unMontable.position(posicionMontableInicial)
		unMontable.moverse()
		assert.equals(rana1P.position(), posicionMontableInicial)
	}

	test "Un movible vuelve al principio al superar el offset del tablero" {
		game.width(14)
		game.height(16)
		game.addVisual(unMontable)
		const posicionBorde = game.at(0 - izquierda.limiteOffsetX(), 0)
		const posicionDeRetorno = game.at(game.width(), 0)
		unMontable.position(posicionBorde)
		unMontable.moverse()
		assert.equals(unMontable.position(), posicionDeRetorno)
	}

	test "Al morir la rana vuelve a la posicion inicial" {
		game.width(14)
		game.height(16)
		game.addVisual(rana1P)
		rana1P.position(game.at(-4, 2))
		rana1P.morir()
		assert.equals(rana1P.position(), rana1P.posicionInicial())
	}

}

